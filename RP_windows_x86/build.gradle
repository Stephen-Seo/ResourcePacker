
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersionName()

apply plugin: 'cpp'

model {
    buildTypes {
        debug
        release
    }

    platforms {
        windows_x86 {
            operatingSystem "windows"
            architecture "x86"
        }
    }

    toolChains {
        mingw_x86(Gcc) {
            path "/usr/bin"
            eachPlatform() {
                cCompiler.executable "i686-w64-mingw32-gcc"
                cppCompiler.executable "i686-w64-mingw32-g++"
                linker.executable "i686-w64-mingw32-g++"
                assembler.executable "i686-w64-mingw32-as"
                staticLibArchiver.executable "i686-w64-mingw32-ar"
            }
            target("windows_x86")
        }
    }
}

libraries {
    ResourcePacker {
        targetPlatforms "windows_x86"

        binaries.all {
            cppCompiler.args "-std=c++11", "-Wall", "-Wextra"

            if(buildType == buildTypes.debug) {
                cppCompiler.args "-O0", "-g"
            }
            if(buildType == buildTypes.release) {
                cppCompiler.define "NDEBUG"
                cppCompiler.args "-O3"
            }
        }
        binaries.withType(SharedLibraryBinarySpec) { binary ->
            cppCompiler.define "BUILD_SHARED_LIBS"
            cppCompiler.define "ResourcePacker_EXPORTS"
            def importLibPath = binary.sharedLibraryLinkFile.absolutePath.replace(".dll", ".a")
            importLibPath = importLibPath.substring(0, importLibPath.lastIndexOf("ResourcePacker")) + "lib" + importLibPath.substring(importLibPath.lastIndexOf("ResourcePacker"))
            //println "Generating import lib: ${importLibPath}"
            linker.args "-Wl,--out-implib,${importLibPath}"
        }
    }
}

executables {
    RP_main {
        targetPlatforms "windows_x86"

        binaries.all {
            cppCompiler.args "-std=c++11", "-Wall", "-Wextra"

            if(buildType == buildTypes.debug) {
                cppCompiler.args "-O0", "-g"
            }
            if(buildType == buildTypes.release) {
                cppCompiler.define "NDEBUG"
                cppCompiler.args "-O3"
            }
        }
    }
}

sources {
    ResourcePacker {
        cpp {
            source {
                srcDir "${projectDir}/../src/ResourcePacker/cpp"
                include "*.cpp"
            }
            exportedHeaders {
                srcDir "${projectDir}/../src/ResourcePacker/headers"
            }
        }
    }
    RP_main {
        cpp {
            lib libraries.ResourcePacker
            source {
                srcDir "${projectDir}/../src/RP_main/cpp"
                include "*.cpp"
            }
            exportedHeaders {
                srcDir "${projectDir}/../src/ResourcePacker/headers"
            }
        }
    }
}

